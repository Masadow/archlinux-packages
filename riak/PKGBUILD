# Maintainer: Vincent Bernardoff <vb@luminar.eu.org>
# Contributor: lafka <lafa@hackeriet.no>
# Contributor: Albert Chang <albert.chang@gmx.com>
# Contributor: Thomas Mudrunka <harvie@@email..cz> You can also contact me on http://blog.harvie.cz/
# Contributor: zhehao

pkgname=riak
pkgver=2.0.0beta1
pkgrel=1
pkgdesc='NOSQL database engine providing decentralized key-value store, flexible map/reduce engine and HTTP/JSON query interface'
arch=('i686' 'x86_64')
license=('APACHE')
url='http://riak.basho.com/'
makedepends=('erlang-nox<17' 'java-environment')
backup=('opt/riak/etc/riak.conf')
install='riak.install'
source=(
    "http://s3.amazonaws.com/downloads.basho.com/${pkgname}/2.0/${pkgver}/${pkgname}-${pkgver}.tar.gz"
    'riak.service'
)
md5sums=('88a75d6e1714ebfe4f5221dc5a00bf6a'
         'ed4cda47fc1fad8b0a3fb3858600c578')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  unset LDFLAGS
  make rel
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/opt"
  install -d "$pkgdir/var/log"
  install -d "$pkgdir/etc"
  install -d "$pkgdir/usr/share/doc"

  cp -a "rel/riak" "$pkgdir/opt/riak"
  chmod -R 755 "$pkgdir/opt/riak/bin"
  ln -s /opt/riak/log "$pkgdir/var/log/riak"
  ln -s /opt/riak/etc "$pkgdir/etc/riak"
  cp -R "doc/man" "$pkgdir/usr/share"
  cp -R "doc" "$pkgdir/usr/share/doc/riak"
  rm -R "$pkgdir/usr/share/doc/riak/man"
  chmod -R 755 "$pkgdir/usr/share"

  # install daemon
  install -Dm644 "$srcdir"/riak.service "$pkgdir"/usr/lib/systemd/system/riak.service
}
