# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=flatbuffers
pkgver=1.0.3
pkgrel=1
pkgdesc="An efficient cross platform serialization library for C++, with support for Java, C# and Go"
arch=('i686' 'x86_64')
url="http://google.github.io/flatbuffers/"
license=('Apache')
groups=()
depends=()
makedepends=(cmake)
source=(https://github.com/google/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('0c0a4db2cb9f17ba6a0138948da893fe')


build() {
	cd "$srcdir/$pkgname-$pkgver"
	cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make test
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
  _docdir="$pkgdir/usr/share/doc/$pkgname"  
  install -m755 -d $_docdir
  cp -a docs/*.html docs/html $_docdir
}
