# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=mesos
pkgver=0.20.1
pkgrel=2
pkgdesc="A cluster manager that simplifies the complexity of running applications on a shared pool of servers"
arch=('i686' 'x86_64')
url='http://mesos.apache.org/'
license=('Apache')
groups=('science')
depends=('python2' 'python2-boto' 'curl' 'libsasl' 'leveldb' 'java-environment' 'libunwind' 'python2-protobuf')
makedepends=('java-environment' 'maven' 'http-parser' 'python2-http-parser' 'protobuf-java' 'protobuf'
'google-glog' 'gperftools')
source=("http://www.apache.org/dist/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname-master.service"
  "$pkgname-slave.service"
  'configure.ac.gcc49.patch')
md5sums=('8ae8ea79a07fd86f02b8e2b7b8b7a1db'
         'da6ff9d8a2df96325bee325a957eb760'
         '69df716316170056ff2a54c5299d8cb4'
         'ea3e77b0b75bf0f6d5015d36485b5243')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
  if [ ! -d build ]; then
    mkdir build
  fi

  patch -p0 --verbose -i $srcdir/configure.ac.gcc49.patch
  sed --in-place -e 's/python -c/python2 -c/g' configure m4/ac_python_module.m4
  
  if [ ! -f configure ]; then
    ./bootstrap
  fi
}

build() {
	cd "$srcdir/$pkgname-$pkgver"/build
  PYTHON_VERSION=2.7 \
  PROTOBUF_JAR=/usr/share/java/protobuf-java/protobuf-java-2.5.0.jar \
  LIBS='-lprotobuf -lglog' \
  ../configure \
   --exec-prefix=/usr \
   --with-protobuf=/usr \
   --with-glog=/usr \
   --with-leveldb=/usr \
   --with-gperftools=/usr

	make
}

check() {
  echo "skipping 'make check'."
  # (skip tests because some tests don't pass)
  #cd "$srcdir/$pkgname-$pkgver"/build
  #make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"/build
	make DESTDIR="$pkgdir/" install
  mkdir -p -m755 $pkgdir/usr/share/java/$pkgname 
  mkdir -p -m755 $pkgdir/var/{lib,log}/$pkgname
  install -m644 ./src/java/mesos.pom $pkgdir/usr/share/java/$pkgname/
  install -m644 ./src/java/target/*.jar $pkgdir/usr/share/java/$pkgname/
  mkdir -p -m755 $pkgdir/usr/lib/systemd/system
  install -m644 $srcdir/$pkgname-{master,slave}.service $pkgdir/usr/lib/systemd/system
  
  # python
  cd ./src/python
  python2 setup.py install --root="$pkgdir" --optimize=1
}
