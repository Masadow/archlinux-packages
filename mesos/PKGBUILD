# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=mesos
pkgver=0.19.1
pkgrel=1
pkgdesc="A cluster manager that simplifies the complexity of running applications on a shared pool of servers"
arch=('x86_64')
url='http://mesos.apache.org/'
license=('Apache')
groups=('science')
depends=('python2' 'python2-boto' 'curl' 'libsasl' 'leveldb' 'java-environment' 'libunwind')
makedepends=('jdk7-openjdk' 'maven' 'clang' 'http-parser' 'python-http-parser' )
source=("http://www.apache.org/dist/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
"$pkgname-master.service"
"$pkgname-slave.service"
)
md5sums=('838841148d933ba21e205ae7e75b1821'
         'da6ff9d8a2df96325bee325a957eb760'
         '69df716316170056ff2a54c5299d8cb4')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
  if [ ! -f configure ]; then
    ./bootstrap
  fi
  if [ ! -d build ]; then
    mkdir build
  fi
  sed -i 's/python -c/python2 -c/g' configure
}

build() {
	cd "$srcdir/$pkgname-$pkgver"/build
  CC=clang CXX=clang++ LDFLAGS=-ldl ../configure --with-leveldb=/usr --prefix=/usr --libexecdir=/usr/lib
	make
}

check() {
  echo "skipping 'make check'."
  # (skip tests because some tests don't pass)
  #	cd "$srcdir/$pkgname-$pkgver"/build
  #	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"/build
	make DESTDIR="$pkgdir/" install
  mv $pkgdir/usr/var/$pkgname/* $pkgdir/usr/share/$pkgname/
  rm -rf $pkgdir/usr/var
  mkdir -p -m755 $pkgdir/usr/share/java/$pkgname 
  mkdir -p -m755 $pkgdir/var/{lib,log}/$pkgname
  install -m644 ./src/java/mesos.pom $pkgdir/usr/share/java/$pkgname/
  install -m644 ./src/java/target/*.jar $pkgdir/usr/share/java/$pkgname/
  mkdir -p -m755 $pkgdir/usr/lib/systemd/system
  install -m644 $srcdir/$pkgname-{master,slave}.service $pkgdir/usr/lib/systemd/system
  
  # python
  cd ./src/python
  python2 setup.py install --root="$pkgdir" --optimize=1
}
